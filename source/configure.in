#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ([2.63])
AC_INIT([modylas], [1.0.0], [modylas@localhost])
AM_INIT_AUTOMAKE([foreign])
AC_CONFIG_SRCDIR([src/param.h])
AC_CONFIG_HEADERS([config.h])

# Checks for programs.
AC_MSG_CHECKING([enabling mpi])
AC_ARG_ENABLE([mpi],
	AC_HELP_STRING([--disable-mpi], [MPI is enable without this option]),
	[], [enable_mpi=yes])
AC_MSG_RESULT([$enable_mpi])
if test $enable_mpi = yes; then
	MPI="-DMPIPARA"
else
	MPI=""
fi
AC_ARG_VAR([FC], [Fortran90 compiler command])
AC_ARG_VAR([FCFLAGS], [Fortran90 compiler flags])
AM_CONDITIONAL([HAVE_MPI], [test $enable_mpi = yes])
AC_ARG_WITH([kind-fortran-compiler],
	AC_HELP_STRING([--with-kind-fortran-compiler=FC],
		[FC=(K|FX10|INTEL|PGI)]),
		[], [with_kind_fortran_compiler=unset]
	)
COMDEFS="-DCOMM_CUBE -DFJMPIDIR -DSYNC_COM #-DDLVO #-DSEGSHAKE #-DSEPARATE_FCE #-DVIRPARTS"
case "$with_kind_fortran_compiler" in
	K | FX10)
		PRESET_FC=mpifrtpx
		PRESET_FCFLAGS="$MPI -Kopenmp,fast,parallel,array_private,auto,simd=2,ocl -Cpp -Kilfunc,ocl,preex,NOFLTLD,simd=2,mfunc=2 -DOCTA_CORE $COMDEFS"
	;;
	INTEL)
		PRESET_FC=mpif90
		PRESET_FCFLAGS="$MPI -openmp -traceback -DOCTA_CORE -fpp -O3 $COMDEFS"
	;;
	PGI)
		PRESET_FC=mpif90
		PRESET_FCFLAGS="$MPI -mp -fastsse -tp x64 -Mpreprocess $COMDEFS"
	;;
esac
#
AC_MSG_CHECKING([for fortran compiler])
if test x$FC = x; then
	FC=$PRESET_FC
fi
if test x$FC = x; then
	AC_MSG_RESULT([unknown])
	AC_MSG_ERROR([You need decide fortran compiler.])
else
	AC_MSG_RESULT([$FC])
fi
#
AC_MSG_CHECKING([for fortran compiler options])
if test "x$FCFLAGS" = x; then
	FCFLAGS=$PRESET_FCFLAGS
fi
if test "x$FCFLAGS" = x; then
	AC_MSG_RESULT([unset])
	AC_MSG_ERROR([You need set fortran compiler options.])
else
	AC_MSG_RESULT([$FCFLAGS])
fi
#
AC_PROG_CC      # To define "OBJEXT"
AC_SUBST([F77])  # dummy description for automake

# Checks for libraries.

# Checks for header files.

# Checks for typedefs, structures, and compiler characteristics.

# Checks for library functions.

AC_CONFIG_FILES([Makefile
	src/Makefile
	src/Doxyfile
	])
AC_OUTPUT
